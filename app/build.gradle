plugins {
    id 'com.android.application'
}

// key, hash value 등 github에 숨겨서 올리기 위해 local.properties 사용
// local.properties 에서 선언한 값들을 불러오기 위함
Properties properties = new Properties()
properties.load(project.rootProject.file('local.properties').newDataInputStream())
// manifest에서 사용하기 위해 값을 선언해둠
def AD_ID = properties.getProperty('admobs_id')

android {
    namespace 'development.app.accountbook'
    compileSdk 33

    defaultConfig {
        applicationId "development.app.accountbook"
        minSdk 21
        targetSdk 33
        versionCode 7
        // versionName : 보통 <major>, <minor>, <point> 순으로 표기
        // major : 앱의 큰 틀이 변화된 경우, major : 기능 추가 삭제 등 사양이 변경될 때, point : 버그와 디자인 수정 등
        versionName "2.4.3"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // 프로젝트 내에서 사용 가능한 변수, 타입 - 키 - 값 으로 저장
        buildConfigField "String", "serverAddress", properties['serverAddress']
        buildConfigField "String", "admobs_ad_id", properties['admobs_ad_id']

        // 매니페스트에서 사용하는 키 설정, 키 - 값 으로 저장
        manifestPlaceholders = [AD_ID: AD_ID]
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dataBinding {
        enabled = true
    }
}

dependencies {
    // MaterialCalendarView : 달력에 주말 색 바꾸고 선택 시 색 변경하기위한 외부 라이브러리 > 커스텀 달력 라이브러리
    implementation 'com.prolificinteractive:material-calendarview:1.4.3'

    // Retrofit 라이브러리
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    // Gson 변환기 라이브러리
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'

    // google 광고 라이브러리
    implementation 'com.google.android.gms:play-services-ads:22.1.0'

    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.navigation:navigation-fragment:2.5.3'
    implementation 'androidx.navigation:navigation-ui:2.5.3'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}